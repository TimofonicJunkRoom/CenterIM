AC_INIT(src/centericq.cc)
AM_INIT_AUTOMAKE(centericq, 4.8.8)

AC_CANONICAL_HOST

AC_PROG_CC
AC_PROG_CXX
AC_PROG_CPP
AC_PROG_RANLIB

AC_LANG_SAVE
AC_LANG_CPLUSPLUS

AM_CONFIG_HEADER(config.h)

###     use option --enable-locales-fix to fix problems with double-byte
###     encodings on systems with broken locale, iscntrl()

AC_ARG_ENABLE(locales-fix,
	[  --enable-locales-fix    enables workaround for broken locales. use this
			  option if some of characters in your texts in Chinese
			  or Japanese diappear])

###     sigc++ check

AM_PATH_SIGC(1.0.0,
    [CXXFLAGS="$CXXFLAGS $SIGC_CFLAGS" LIBS="$LIBS $SIGC_LIBS"],
    [AC_MSG_ERROR([You need to have the libsigc++ libraries installed - see the README for more information])])

###     use option --with-fribidi to compile with fribidi support
AC_ARG_WITH(fribidi,
	[  --with-fribidi=[DIR]    enable Hebrew and Arabic support using fribidi
			  library in DIR],
	[with_fribidi=$withval],
	[with_fribidi=no])

if test "$with_fribidi" = "yes"; then
    for ac_dir in /usr/local /usr; do
	if test -f "$ac_dir/include/fribidi/fribidi.h"; then
	    with_fribidi=$ac_dir
	    break;
	fi
    done
fi

if test -n "$with_fribidi" -a "$with_fribidi" != "no"; then
    if test "$with_fribidi" = "yes"; then with_fribidi="/usr"; fi

    if test -r $with_fribidi/include/fribidi/fribidi.h; then
	LIBS="$LIBS `$with_fribidi/bin/fribidi-config --libs`"
	AC_DEFINE(USE_FRIBIDI)
    fi
fi

AC_CHECK_HEADERS([vector iostream fstream string algorithm list memory iterator exception utility],,
  AC_MSG_ERROR([You need to have the libstdc++ headers installed]))

AC_CHECK_HEADERS([hash_map strstream ext/hash_map sstream],,)

AC_CHECK_HEADERS(ext/hash_map,,
  AC_CHECK_HEADERS(hash_map,, AC_MSG_ERROR([Your libstdc++ installation does not have the extra hash classes])))

AC_CHECK_HEADERS(sstream,,
  AC_CHECK_HEADERS(strstream,, AC_MSG_ERROR([Your libstdc++ doesn't have the sstream or strstream classes])))

AC_CHECK_HEADERS(iconv.h)

AC_CHECK_LIB(iconv, iconv,,
    [AC_CHECK_LIB(c, iconv,, AC_MSG_WARN(libiconv not found - Russian codepages translation in MSN will not work))])

AC_CHECK_LIB(socket, socket)
AC_CHECK_LIB(nsl, gethostbyname)

AC_CHECK_LIB(ncurses, initscr,,
    [AC_CHECK_LIB(curses, initscr,, AC_MSG_ERROR(The ncurses terminal library is required in order to build the program))])

AC_LANG_RESTORE

AM_PATH_GLIB(1.2.0, [ CXXFLAGS="$CXXFLAGS $GLIB_CFLAGS" ], AC_MSG_ERROR([glib is required in order to compile centericq - download it from ftp://ftp.gtk.org/pub/gtk]))

case "$host_os" in
	darwin*)
	    LIBS=`for i in $LIBS; do echo $i | grep -v pthread; done`
	    LIBS="$LIBS -L/sw/lib -flat_namespace"
	    CXXFLAGS="$CXXFLAGS $GLIB_CFLAGS -I/sw/include"
	;;
esac

AC_CONFIG_SUBDIRS(kkconsui-0.1 kkstrtext-0.1 kksystr-0.1 libicq2000-0.3.3 libmsn-0.1 firetalk-0.1 libyahoo2-0.1 libjabber-0.1)
ALL_LINGUAS="zh_TW.Big5 uk sv ru ro pt_BR pl nl ms it hu fr es de cs"
AM_GNU_GETTEXT
AC_OUTPUT(Makefile contrib/Makefile contrib/spanish/Makefile misc/Makefile share/Makefile src/Makefile src/hooks/Makefile po/Makefile.in intl/Makefile)

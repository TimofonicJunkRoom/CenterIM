AC_INIT(aclocal.m4)
AM_INIT_AUTOMAKE(centericq, 4.6.6)

AC_PROG_CC
AC_PROG_CXX
AC_PROG_CPP
AC_PROG_RANLIB

AM_CONFIG_HEADER(config.h)

###     use option --enable-locales-fix to fix problems with double-byte
###     encodings on systems with broken locale, iscntrl()

AC_ARG_ENABLE(locales-fix,
	[  --enable-locales-fix    enables workaround for broken locales. use this
			  option if some of characters in your texts in Chinese
			  or Japanese diappear])

###	sigc++ check

AM_PATH_SIGC(1.0.0,
    [CXXFLAGS="$CXXFLAGS $SIGC_CFLAGS"],
    [AC_MSG_ERROR([You need to have the libsigc++ libraries installed - see the README for more information])])

###     use option --with-fribidi to compile with fribidi support
AC_ARG_WITH(fribidi,
	[  --with-fribidi=[DIR]    enable Hebrew and Arabic support using fribidi
			  library in DIR],
	[with_fribidi=$withval],
	[with_fribidi=no])

if test "$with_fribidi" = "yes"; then
    for ac_dir in /usr/local /usr; do
	if test -f "$ac_dir/include/fribidi/fribidi.h"; then
	    with_fribidi=$ac_dir
	    break;
	fi
    done
fi

if test -n "$with_fribidi" -a "$with_fribidi" != "no"; then
    if test "$with_fribidi" = "yes"; then with_fribidi="/usr"; fi

    if test -r $with_fribidi/include/fribidi/fribidi.h; then
	LIBS="$LIBS `$with_fribidi/bin/fribidi-config --libs`"
    fi
fi

AC_LANG_SAVE
AC_LANG_CPLUSPLUS

AC_CHECK_HEADERS([vector iostream fstream string algorithm list memory iterator exception utility],,
  AC_MSG_ERROR([You need to have the libstdc++ headers installed]))

AC_CHECK_HEADERS([hash_map strstream ext/hash_map sstream],,)

AC_CHECK_HEADERS(ext/hash_map,,
  AC_CHECK_HEADERS(hash_map,, AC_MSG_ERROR([Your libstdc++ installation does not have the extra hash classes])))

AC_CHECK_HEADERS(sstream,,
  AC_CHECK_HEADERS(strstream,, AC_MSG_ERROR([Your libstdc++ doesn't have the sstream or strstream classes])))

AC_LANG_RESTORE

AC_CHECK_HEADERS(iconv.h)

AC_CHECK_LIB(ncurses, main, LIBS="$LIBS -lncurses", LIBS="$LIBS -lcurses")

AC_CONFIG_SUBDIRS(kkstrtext-0.1 kkconsui-0.1 kksystr-0.1 libyahoo-0.1 libmsn-0.1 libicq2000-0.5)
ALL_LINGUAS="zh_TW.Big5 ru ro pt_BR pl nl it hu es de cs"
AM_GNU_GETTEXT
AC_OUTPUT(Makefile contrib/Makefile contrib/spanish/Makefile misc/Makefile share/Makefile src/Makefile src/hooks/Makefile po/Makefile.in intl/Makefile)
